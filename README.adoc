:source-highlighter: coderay
:source-language: clojure
:toc:
:toc-placement: preamble
:sectlinks:
:sectanchors:
:sectnums:
ifdef::env-github,env-cljdoc[]
:tip-caption: :bulb:
:note-caption: :information_source:
:important-caption: :heavy_exclamation_mark:
:caution-caption: :fire:
:warning-caption: :warning:
endif::[]

= EQL

image:https://img.shields.io/clojars/v/edn-query-language/eql.svg[link=https://clojars.org/edn-query-language/eql]
image:https://cljdoc.xyz/badge/edn-query-language/eql[link=https://cljdoc.xyz/d/edn-query-language/eql/CURRENT]
image:https://circleci.com/gh/edn-query-language/eql/tree/master.svg?style=svg["CircleCI", link="https://circleci.com/gh/edn-query-language/eql/tree/master"]

This repository contains a specification for EQL and a support library to help the development
of tools using it.

== What is EQL?

EQL is a declarative way to make hierarchical (and possibly nested) selections of information about data requirements.

EQL doesn't have it's own syntax, it uses EDN to express the request, taking advantage of
the rich set of primitives provided by it.

==== EQL for selections

An easy way to get started is to think of a nested map and try to describe it's shape. For example:

[source,clojure]
----
{:album/name "Magical Mystery Tour"
 :album/year 1967}
----

By describing the shape we mean describing the structure but without the values, the previous
example can be described using EQL as:

[source,clojure]
----
[:album/name :album/year]
----

Like using `select-keys` to specify which fields to extract from a map. Now let's see
what it looks like for nested structures:

[source,clojure]
----
{:album/name   "Magical Mystery Tour"
 :album/artist {:artist/name "The Beatles"}}

; can be described as:

[:album/name
 ; note the use of a map to express nesting
 {:album/artist
  [:artist/name]}]
----

It works the same to represent nested lists:

[source,clojure]
----
{:album/name   "Magical Mystery Tour"
 :album/tracks [{:track/name "The Fool On The Hill"}
                {:track/name "All You Need Is Love"}]}

; can be described as:

[:album/name
 {:album/tracks
  [:track/name]}]
----

==== EQL for operations

EQL also supports `mutations`, which are like side effect calls to an API. Mutations can
appear at the root of some EQL transaction and they look like Clojure function calls, example:

[source,clojure]
----
[(call-mutation {:data "value"})]
----

More details in <<Mutations>>.

=== Datomic Pull Syntax comparison

On top of the Datomic Pull Syntax expression, EQL also supports:

- Parametrization
- <<Mutations>>
- <<Unions,Union Queries>>

Check the links on each for more details.

The link:https://docs.datomic.com/on-prem/pull.html#attribute-with-options[attribute with options] feature
from the Datomic Pull Syntax is not present in EQL, instead we provide the parameterized
attributes that can handle arbitrary data to go along the base property.

=== GraphQL comparison

Similar to GraphQL, EQL works as a language to client libraries to communicate data requirements and operations,
there is a good set of intersection in features between both, as:

- a language to describe arbirtrarly nested structures
- support for mutations (operations to side effect the world)
- support for parametrization
- union queries for heterogeneos lists

GraphQL has a type system in its definition and it is required for a GraphQL system to work. EQL has
no such thing, and it dictates only the syntax but not the semantics. Some features in
GraphQL just doesn't make sense in EQL, like fragments, since EQL is already a data format (EDN),
it's easy to compose data as you would do in a regular Clojure program, for that reason
many features are not nescessary because EQL is a parseable data format with all the Clojure
tools already available to operate on top of it. Also read the <<AST Encode/Decode,AST>> section for more information
on how to programatically manipulate EQL data structures.

== Specification

The following sections will explain the features provided by the EQL syntax and the
common usages of it. Along with code examples from the transactions, after that we
are going to provide the AST data that's equivalent to the transaction, the AST the
recommended format to transform EQL transactions programatically. The EQL format is
great for users and interfaces, but to transform is easier if you don't have to deal
with the syntax details. The EQL library provides functions to convert from and to
AST from EQL.

=== Query / Transactions

A EQL transaction is represented by an EDN vector.

Examples:

[source,clojure]
----
[] ; empty transaction

; ast

{:type :root, :children []}
----

A transaction that only contains reads is commonly called a `query`, but notice that
at syntax level it has no difference.

=== Properties

Properties in `EQL` are expressed as Clojure keywords, they can be simple or qualified
keywords, and they express the property been requested.

Examples:

[source,clojure]
----
[:album/name :album/year]

; ast

{:type     :root
 :children [{:type :prop, :dispatch-key :album/name, :key :album/name}
            {:type :prop, :dispatch-key :album/year, :key :album/year}]}
----

=== Joins

Joins are used to describe nesting in the request transaction. They are represented as
EDN maps, always with a single entry, the entry key is the property to join on, and the
entry value is a sub-query to run.

Examples:

[source,clojure]
----
[{:favorite-albums
  [:album/name :album/year]}]
  
; ast

{:type     :root
 :children [{:type         :join
             :dispatch-key :favorite-albums
             :key          :favorite-albums
             :query        [:album/name :album/year]
             :children     [{:type :prop, :dispatch-key :album/name, :key :album/name}
                            {:type :prop, :dispatch-key :album/year, :key :album/year}]}]}
----

Nested joins example:

[source,clojure]
----
[{:favorite-albums
  [:album/name :album/year
   {:album/tracks
    [:track/name
     :track/duration]}]}]
     
; ast

{:type :root
 :children
 [{:type         :join
   :dispatch-key :favorite-albums
   :key          :favorite-albums

   :query        [:album/name
                  :album/year
                  {:album/tracks [:track/name :track/duration]}]

   :children     [{:type :prop, :dispatch-key :album/name, :key :album/name}
                  {:type :prop, :dispatch-key :album/year, :key :album/year}
                  {:type         :join
                   :dispatch-key :album/tracks
                   :key          :album/tracks
                   :query        [:track/name :track/duration]
                   :children     [{:type :prop, :dispatch-key :track/name, :key :track/name}
                                  {:type         :prop
                                   :dispatch-key :track/duration
                                   :key          :track/duration}]}]}]}
----

=== Idents

Idents are represented by a vector with two elements, where the first is a keyword and
the second can be anything. They are like link:http://blog.datomic.com/2014/02/datomic-lookup-refs.html[lookup refs on Datomic],
in general they can provide an address-like thing, and their use and semantic might
vary from system to system.

Examples:

[source,clojure]
----
[[:customer/id 123]]

; ast

{:type :root
 :children [{:type :prop, :dispatch-key :customer/id, :key [:customer/id 123]}]}
----

Note that this time in the AST the `:dispatch-key` and `:key` got different values this
time, the `:dispatch-key` been just the `ident key` while the `:key` contains the
full thing.

It's common to use an ident as a join key to start a query for some entity:

[source,clojure]
----
[{[:customer/id 123]
  [:customer/name :customer/email]}]
  
; ast

{:type     :root
 :children [{:type         :join
             :dispatch-key :customer/id
             :key          [:customer/id 123]
             :query        [:customer/name :customer/email]
             :children     [{:type :prop, :dispatch-key :customer/name, :key :customer/name}
                            {:type         :prop
                             :dispatch-key :customer/email
                             :key          :customer/email}]}]}
----

=== Parameters

EQL properties, joins and idents have a support for parametrization. This allows the
query to provide an extra dimension of information about the requested data. A parameter
is expressed by wrapping the thing with a EDN list, like so:

[source,clojure]
----
; without params
[:foo]

; with params
[(:foo {:with "params"})]

; ast

{:type     :root
 :children [{:type         :prop
             :dispatch-key :foo
             :key          :foo
             :params       {:with "params"}
             :meta         {:line 1, :column 15}}]}
----

Note on the AST side it gets a new `:params` key. Params *must* be always maps, the
map values can be anything. Here are more examples of parameterizing queries:

[source,clojure]
----
; ident with params

[([:ident "value"] {:with "param"})]

{:type     :root
 :children [{:type         :prop
             :dispatch-key :ident
             :key          [:ident "value"]
             :params       {:with "param"}
             :meta         {:line 1, :column 15}}]}

; join with params wrap the key with the list

[{(:join-key {:with "params"})
  [:sub-query]}]

{:type     :root
 :children [{:type         :join
             :dispatch-key :join-key
             :key          :join-key
             :params       {:with "params"}
             :meta         {:line 1, :column 16}
             :query        [:sub-query]
             :children     [{:type         :prop
                             :dispatch-key :sub-query
                             :key          :sub-query}]}]}

; ident join with params

[{([:ident "value"] {:with "params"})
  [:sub-query]}]
  
{:type     :root
 :children [{:type         :join
             :dispatch-key :ident
             :key          [:ident "value"]
             :params       {:with "params"}
             :meta         {:line 1 :column 16}
             :query        [:sub-query]
             :children     [{:type         :prop
                             :dispatch-key :sub-query
                             :key          :sub-query}]}]}

; alternate syntax to add params on joins (wrap the entire map, AST result is the same)

[({:join-key
   [:sub-query]}
  {:with "params"})]
  
{:type     :root
 :children [{:type         :join
             :dispatch-key :join-key
             :key          :join-key
             :params       {:with "params"}
             :meta         {:line 1, :column 16}
             :query        [:sub-query]
             :children     [{:type         :prop
                             :dispatch-key :sub-query
                             :key          :sub-query}]}]}
----

WARNING: remember that unless you are writing on some EDN file, you have to quote the
lists or Clojure will try to execute then, EQL expects then to come as data lists.

=== Query Meta

Meta data from the query is stored in the AST and restore on the other way around. This
allows to add an extra meta layer of information at query level.

[source,clojure]
----
(with-meta [] {:meta "data"})

; ast

{:type :root, :children [], :meta {:meta "data"}}
----

=== Unions

In EQL unions are used to specify polymorphic requirements, that means depending on some
condition a different query might be choosen to fulfill the requirements. For example,
in a messaging the app you have a single list, each entry on the chat log can be a `message`,
`audio` or `photo`, each own having it query requirement. Here is it in code:

[source,clojure]
----
; message query
[:message/id :message/text :chat.entry/timestamp]

; audio query
[:audio/id :audio/url :audio/duration :chat.entry/timestamp]

; photo query
[:photo/id :photo/url :photo/width :photo/height :chat.entry/timestamp]

; list query
[{:chat/entries ???}] ; what goes there?
----

Now to express this polymorphic requirement as the sub-query of the `:chat/entries` list
we can use a map as the join value, and each entry on this map represents a possible
sub-query, the way this information is used is up the parser implementation, EQL only
defines the syntax, here are some examples of how it could be written:

[source,clojure]
----
; in this example the selection is made by looking if the processed entry contains
; some value on the key used for its selection
[{:chat/entries
  {:message/id [:message/id :message/text :chat.entry/timestamp]
   :audio/id   [:audio/id :audio/url :audio/duration :chat.entry/timestamp]
   :photo/id   [:photo/id :photo/url :photo/width :photo/height :chat.entry/timestamp]}}]

; in this case we give a type name and use as the key, this usually requires some
; out of band configuration to know how to pull this data from each entry to use
; as the comparison
[{:chat/entries
  {:entry.type/message [:message/id :message/text :chat.entry/timestamp]
   :entry.type/audio   [:audio/id :audio/url :audio/duration :chat.entry/timestamp]
   :entry.type/photo   [:photo/id :photo/url :photo/width :photo/height :chat.entry/timestamp]}}]

; ast for first example

{:type :root
 :children
 [{:type         :join
   :dispatch-key :chat/entries
   :key          :chat/entries
   :query        {:message/id [:message/id :message/text :chat.entry/timestamp]
                  :audio/id   [:audio/id :audio/url :audio/duration :chat.entry/timestamp]
                  :photo/id   [:photo/id
                               :photo/url
                               :photo/width
                               :photo/height
                               :chat.entry/timestamp]}
   :children     [{:type :union
                   :query
                         {:message/id [:message/id :message/text :chat.entry/timestamp]
                          :audio/id   [:audio/id :audio/url :audio/duration :chat.entry/timestamp]
                          :photo/id   [:photo/id
                                       :photo/url
                                       :photo/width
                                       :photo/height
                                       :chat.entry/timestamp]}
                   :children
                         [{:type      :union-entry
                           :union-key :message/id
                           :query     [:message/id :message/text :chat.entry/timestamp]
                           :children  [{:type :prop, :dispatch-key :message/id, :key :message/id}
                                       {:type :prop, :dispatch-key :message/text, :key :message/text}
                                       {:type         :prop
                                        :dispatch-key :chat.entry/timestamp
                                        :key          :chat.entry/timestamp}]}
                          {:type      :union-entry
                           :union-key :audio/id
                           :query     [:audio/id :audio/url :audio/duration :chat.entry/timestamp]
                           :children  [{:type :prop, :dispatch-key :audio/id, :key :audio/id}
                                       {:type :prop, :dispatch-key :audio/url, :key :audio/url}
                                       {:type         :prop
                                        :dispatch-key :audio/duration
                                        :key          :audio/duration}
                                       {:type         :prop
                                        :dispatch-key :chat.entry/timestamp
                                        :key          :chat.entry/timestamp}]}
                          {:type      :union-entry
                           :union-key :photo/id
                           :query     [:photo/id
                                       :photo/url
                                       :photo/width
                                       :photo/height
                                       :chat.entry/timestamp]
                           :children  [{:type :prop, :dispatch-key :photo/id, :key :photo/id}
                                       {:type :prop, :dispatch-key :photo/url, :key :photo/url}
                                       {:type :prop, :dispatch-key :photo/width, :key :photo/width}
                                       {:type :prop, :dispatch-key :photo/height, :key :photo/height}
                                       {:type         :prop
                                        :dispatch-key :chat.entry/timestamp
                                        :key          :chat.entry/timestamp}]}]}]}]}
----

=== Mutations

Mutations in EQL are used to represent operation calls, usually to do something
that will cause a side effect to the world.

A mutation is represented by a list of two elements, the first is the symbol
that names the mutation and the second is a map with input data.

[source,clojure]
----
[(call.some/operation {:data "input"})]

; ast

{:type :root
 :children
 [{:dispatch-key call.some/operation
   :key          call.some/operation
   :params       {:data "input"}
   :meta         {:line 610, :column 17}
   :type         :call}]}
----

NOTE: Mutations and parameters are very like each other, their main difference
is that once uses symbols as keys, the other uses one of the read options (properties,
idents, joins).

Mutations can appear anywhere in the query, root or sub-queries (although if that's supported
by the parser it's up to each implementation).

==== Mutation Joins

A mutation call can also have a join to represent the data to pull from the
mutation output. The syntax is like a join that you do around the mutation:

[source,clojure]
----
[{(call.some/operation {:data "input"})
  [:response :key-a :key-b]}]

; ast

{:type :root
 :children
 [{:dispatch-key call.some/operation
   :key          call.some/operation
   :params       {:data "input"}
   :meta         {:line 612 :column 18}
   :type         :call
   :query        [:response :key-a :key-b]
   :children     [{:type :prop, :dispatch-key :response, :key :response}
                  {:type :prop, :dispatch-key :key-a, :key :key-a}
                  {:type :prop, :dispatch-key :key-b, :key :key-b}]}]}
----

== Library

The package `edn-query-language.core` provides a suite of specs to validate queries and
AST's, it also provides generators for the query and helper functions to common
query operations.

=== Clojure Specs

=== AST Encode/Decode

To convert between query and AST, EQL provides the helper functions `eql/query->ast` and
`eql/ast->query` helper functions. Here are some example usages:

[source,clojure]
----
(eql/query->ast [:foo])
; => {:type :root, :children [{:type :prop, :dispatch-key :foo, :key :foo}]}

(eql/ast->query {:type :root, :children [{:type :prop, :dispatch-key :foo, :key :foo}]})
; => [:foo]
----

=== API Docs

Check complete API docs at linkL:https://cljdoc.org/d/edn-query-language/eql/0.0.3/api/edn-query-language.core[EQL cljdoc page].

//== History
//
//EQL is a later baptized language, it's initial syntax was defined by link:https://docs.datomic.com/pull.html[Datomic Pull Syntax], later extended
//by link:https://github.com/omcljs/om[Om.next].

//== Projects using EQL
//
//- link:http://fulcro.fulcrologic.com/[Fulcro]
//- link:https://github.com/wilkerlucio/pathom[Pathom]
//- link:https://github.com/walkable-server/walkable[Walkable]
